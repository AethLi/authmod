buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id "org.sonarqube" version "2.6"
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "2.1"
group= "io.chocorean.authmod"
archivesBaseName = "authmod"
sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

ext{
    junitVersion= '4.12'
}

configurations {
    embed
    compile.extendsFrom(embed)
}

minecraft {
    version = "1.10.2-12.18.3.2185"
    runDir = "run"
    mappings = "snapshot_20161111"
}

dependencies {
    embed 'org.mariadb.jdbc:mariadb-java-client:2.2.5'
    embed group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
    testCompile "junit:junit:$junitVersion"
}


//Add dependencies to the JAR
jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}